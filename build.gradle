plugins {
    id 'org.springframework.boot' version '2.7.2'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id 'com.google.cloud.tools.jib' version '3.2.1'
    id 'java'
}

group = 'com.dingdongdeng'
version = '1.2.2-RELEASE'
sourceCompatibility = '17'

configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    snippetsDir = file('build/generated-snippets')
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-undertow'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    implementation 'com.auth0:java-jwt:3.18.2' // upbit api 헤더 생성을 위함

    implementation 'com.h2database:h2'
    implementation('it.ozimov:embedded-redis:0.7.3') {
        exclude group: "org.slf4j", module: "slf4j-simple"
    }

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    // 보조 지표 계산을 위함
    implementation 'com.tictactec:ta-lib:0.4.0'

    // test
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

test {
    //fixme 좀 더 우아하게좀....
    String accessKey = findProperty('UPBIT_ACCESS_KEY') ?: 'empty'
    String secretKey = findProperty('UPBIT_SECRET_KEY') ?: 'empty'

    systemProperty "UPBIT_ACCESS_KEY", accessKey
    systemProperty "UPBIT_SECRET_KEY", secretKey

    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    configurations 'asciidoctorExt'
    inputs.dir snippetsDir
    dependsOn test
    doLast {
        copy {
            from "${asciidoctor.outputDir}"
            into 'src/main/resources/static/docs'
        }
    }
}

jib { // https://github.com/GoogleContainerTools/jib/tree/master/jib-gradle-plugin
    from {
        //https://github.com/GoogleContainerTools/jib/tree/master/jib-gradle-plugin#from-closure
        //https://github.com/GoogleContainerTools/jib/tree/master/jib-gradle-plugin#setting-the-base-image
        image = 'eclipse-temurin:17-jre'
    }
    container {
        creationTime = "USE_CURRENT_TIMESTAMP"
    }
}